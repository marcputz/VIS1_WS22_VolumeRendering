precision highp float;
precision mediump sampler3D;

uniform sampler3D data;
uniform sampler2D backSideTexture;
uniform sampler2D frontSideTexture;
uniform int canvasWidth;
uniform int canvasHeight;
uniform float volumeScale;

uniform int raycasting_method;
uniform float iso_value;
uniform int doRefinement;

const int MAX_STEPS = 1024;
const float STEP_SIZE = 0.5;
const int REFINE_STEPS = 8;

vec3 getBackSideCoords(vec2 fragCoords);
vec3 getFrontSideCoords(vec2 fragCoords);
vec3 getRay(vec2 fragCoords);

float raycast_mip(vec3 startPos, vec3 rayDirection, int nSteps);
float raycast_first_hit(vec3 startPos, vec3 rayDirection, int nSteps);

float getDensity(vec3 dataPosition);
vec4 valueToColor(float val);

void main(){

    // get the fragments position
    vec2 fragPos = vec2(gl_FragCoord.x / float(canvasWidth), gl_FragCoord.y / float(canvasHeight));

    // get ray direction and length
    vec3 ray = getRay(fragPos);
    vec3 rayDirection = normalize(ray);
    float rayLength = length(ray);

    // get starting and ending positions of the ray on the bounding box
    vec3 startPos = getFrontSideCoords(fragPos);
    vec3 endPos = getBackSideCoords(fragPos);

    // calculate the number of steps by dividing the length of the ray throught the relative step size
    // IMPORTANT: Step size is given in the volume's coordinates, so it has to be scaled down accordingly first
    int nSteps = int(floor(rayLength / (STEP_SIZE / volumeScale)));
    if (nSteps < 1) {
        discard;
    }

    // sample the data values by the chosen raycasting method
    float density = 0.0;
    if (raycasting_method == 0) {       // MIP = 0
        density = raycast_mip(startPos, rayDirection, nSteps);
    } else {                            // First Hit = 1
        density = raycast_first_hit(startPos, rayDirection, nSteps);
    }

    gl_FragColor = valueToColor(density);

    return;
}

float getDensity(vec3 dataPosition) {
    return texture(data, dataPosition).r;
}

vec4 valueToColor(float val) {
    return vec4(vec3(1.0, 1.0, 1.0) * val, 1.0);
}

vec3 getBackSideCoords(vec2 fragCoords) {
    return texture2D(backSideTexture, fragCoords).xyz;
}

vec3 getFrontSideCoords(vec2 fragCoords) {
    return texture2D(frontSideTexture, fragCoords).xyz;
}

vec3 getRay(vec2 fragCoords) {
    vec3 frontSideCoords = getFrontSideCoords(fragCoords);
    vec3 backSideCoords = getBackSideCoords(fragCoords);

    return (backSideCoords - frontSideCoords);
}

float raycast_mip(vec3 startPos, vec3 rayDirection, int nSteps) {

    float maxVal = -1e6;
    vec3 loc = startPos;
    vec3 maxValLoc;
    vec3 step = (rayDirection * (STEP_SIZE / volumeScale));

    for (int i = 0; i < MAX_STEPS; i++) {
        if (i >= nSteps) {
            break;
        }
        float val = getDensity(loc);
        if (val > maxVal) {
            maxVal = val;
            maxValLoc = loc;
        }
        loc += step;
    }

    if (doRefinement == 1) {
        vec3 refStep = step / float(REFINE_STEPS);
        vec3 refLoc = maxValLoc - refStep * (float(REFINE_STEPS) / 2.0);

        for (int i = 0; i < REFINE_STEPS; i++) {
            maxVal = max(maxVal, getDensity(refLoc));
            refLoc += refStep;
        }
    }

    return maxVal;
}

float raycast_first_hit(vec3 startPos, vec3 rayDirection, int nSteps) {
    return 0.5;
}